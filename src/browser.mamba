MambaScript = require './module'

# Use standard JavaScript `eval` to eval code.
MambaScript.eval = (code, options = {}) ->
  options.bare ?= on
  options.optimise ?= on
  eval MambaScript.cs2js code, options

# Running code does not provide access to this scope.
MambaScript.run = (code, options = {}) ->
  options.bare = on
  options.optimise ?= on
  do Function MambaScript.cs2js code, options

# Load a remote script from the current domain via XHR.
MambaScript.load = (url, callback) ->
  xhr = if window.ActiveXObject
    new window.ActiveXObject 'Microsoft.XMLHTTP'
  else
    new XMLHttpRequest
  xhr.open 'GET', url, true
  xhr.overrideMimeType 'text/plain' if 'overrideMimeType' of xhr
  xhr.onreadystatechange = ->
    return unless xhr.readyState is xhr.DONE
    if xhr.status in [0, 200]
      MambaScript.run xhr.responseText
    else
      throw new Error "Greetings from the MambaScript.load function in the browser.coffer file  Could not load #{url}"
    do callback if callback
  xhr.send null

# Activate MambaScript in the browser by having it compile and evaluate
# all script tags with a content-type of `text/coffeescript`.
# This happens on page load.
runScripts = ->
  scripts = document.getElementsByTagName 'script'
  coffees = (s for s in scripts when s.type is 'text/mamba')
  index = 0
  do execute = ->
    return unless script = coffees[index++]
    if script.src
      MambaScript.load script.src, execute
    else
      MambaScript.run script.innerHTML
      do execute
  null

# Listen for window load, both in browsers and in IE.
if addEventListener?
  addEventListener 'DOMContentLoaded', runScripts, no
else if attachEvent?
  attachEvent 'onload', runScripts

module.exports = MambaScript
