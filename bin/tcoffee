#!/usr/bin/env coffee

fs = require 'fs'
path = require 'path'
CoffeeScript = require '../lib/module'
{exec, spawn} = require 'child_process'

cscodegen = require 'cscodegen'
escodegen = require 'escodegen'

repl = require '../lib/repl'
reporter = require '../lib/reporter'
{debug} = require '../lib/helpers'

{argv} = require('optimist')
  .boolean('compile')
  .boolean('bare')
  .boolean('scope')
  .boolean('self')
  .alias('c', 'compile')
  .alias('e', 'execute')
  .alias('b', 'bare')
  .alias('o', 'out')
  .alias('j', 'join')
  .alias('d', 'debug')

{runMain} = require '../lib/run'

compileFromSourceToJS = (source, {parseOption, compileOption} = {}) ->
  cs_ast = CoffeeScript.parse source, parseOption ? {
    optimise: yes
    raw: true
    inputSource: source
    sourceMap: true
    literate
  }
  js_ast = CoffeeScript.compile cs_ast, compileOption ? bare: !!argv.bare
  escodegen.generate js_ast

run = (fname, source, {parseOption, compileOption} = {}) ->
  cs_ast = CoffeeScript.parse source, parseOption ? {
    optimise: yes
    raw: true
    inputSource: source
    sourceMap: true
    literate
  }
  js_ast = CoffeeScript.compile cs_ast, compileOption ? bare: !!argv.bare
  js = escodegen.generate js_ast
  runMain source, js, js_ast, fname

mode = switch
  when argv.c or argv.compile then 'compile'
  when argv.e or argv.execute then 'execute'
  when argv._.length > 0      then 'execute'
  else 'repl'

class Code
  @join: (codes, to) ->
    c = new Code
      from: (c.from for c in codes)
      to: to
      code: (c.code for c in codes).join('\n')
    c

  constructor: ({@from, @to, @code}) ->

  writeSync: ->
    err = fs.writeFileSync @to, @code
    throw err if err
    console.log 'compile:', @from, '->', @to

  write: (callback) ->
    fs.writeFile @to, @code, (err) =>
      throw err if err
      console.log 'compile:', @from, '->', @to
      callback?()

switch mode
  when 'compile'
    {Preprocessor} = require '../lib/preprocessor'

    literate = argv.literate
    filepaths = argv._

    result = ""

    codes = []
    for fpath in filepaths
      # TODO: check existence
      source = fs.readFileSync(fpath).toString()
      # preprocessed = Preprocessor.process source, {literate}
      try
        cs_ast = CoffeeScript.parse source, {
          optimise: no
          raw: true
          sourceMap: true
          inputSource: source
          literate
        }
      catch e
        console.error 'Error at', fpath
        throw e
        process.exit(1)

      if argv.csast
        console.log cs_ast
        return

      if argv.cscodegen
        console.log cscodegen.generate cs_ast
        return

      js_ast = CoffeeScript.compile cs_ast, bare: !!argv.bare
      code = escodegen.generate js_ast

      outpath =
        if argv.out
          path.join(argv.out or '', (path.basename fpath).replace '.coffee', '.js')
        else
          path.join(fpath.replace '.coffee', '.js')
      codes.push new Code
        from: fpath
        to: outpath
        code: code

    reporter.dump global._root_

    # if argv.join and argv.browserify # TODO
    if argv.join
      console.log codes
      console.log 'join:', argv.join
      outpath = path.join(argv.out or '', argv.join)
      joined = Code.join(codes, outpath)
      joined.write()
    else
      for code in codes
        code.write()

  when 'execute'
    fpath = argv.execute ?  argv._[0]
    source = fs.readFileSync(fpath).toString()
    run fpath, source
    # js = compileFromSourceToJS(source)
    # vm = require 'vm'
    # vm.runInThisContext js, fpath

  when 'repl'
    repl.start()
